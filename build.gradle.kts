/*
 * SPDX-FileCopyrightText: 2026 haskeletor contributors <https://github.com/ForNeVeR/haskeletor>
 *
 * SPDX-License-Identifier: Apache-2.0
 */

import org.jetbrains.intellij.platform.gradle.TestFrameworkType
import org.jetbrains.intellij.platform.gradle.tasks.PrepareSandboxTask

plugins {
    id("scala")
    alias(libs.plugins.intellij.platform)
    alias(libs.plugins.gradle.jvm.wrapper)
}

jvmWrapper {
    linuxAarch64JvmUrl = "https://corretto.aws/downloads/latest/amazon-corretto-17-aarch64-linux-jdk.tar.gz"
    linuxX64JvmUrl = "https://corretto.aws/downloads/latest/amazon-corretto-17-x64-linux-jdk.tar.gz"
    macAarch64JvmUrl = "https://corretto.aws/downloads/latest/amazon-corretto-17-aarch64-macos-jdk.tar.gz"
    macX64JvmUrl = "https://corretto.aws/downloads/latest/amazon-corretto-17-x64-macos-jdk.tar.gz"
    windowsX64JvmUrl = "https://corretto.aws/downloads/latest/amazon-corretto-17-x64-windows-jdk.zip"
}

group = "me.fornever.haskeletor"
version = providers.gradleProperty("pluginVersion").get()

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    implementation(libs.scala.library)

    implementation(libs.spray.json)
    implementation(libs.snakeyaml)
    implementation(libs.scaffeine)
    implementation(libs.directories)
    implementation(libs.fastparse)

    testImplementation(libs.scalatest)
    testImplementation(libs.scalatestplus.junit)

    intellijPlatform {
        intellijIdeaCommunity(providers.gradleProperty("platformVersion"))
        bundledPlugin("com.intellij.java")
        testFramework(TestFrameworkType.Platform)
    }
}

sourceSets {
    main {
        scala {
            srcDirs("src/main/scala", "gen")
        }
    }
    test {
        scala {
            srcDirs("src/test/scala")
        }
    }
}

intellijPlatform {
    pluginConfiguration {
        version = providers.gradleProperty("pluginVersion")
    }
}

tasks {
    named<PrepareSandboxTask>("prepareSandbox") {
        from(listOf("README.md", "LICENSE.txt")) {
            into(pluginName)
        }
    }
    withType<ScalaCompile> {
        scalaCompileOptions.additionalParameters = listOf(
            "-target:jvm-17", "-deprecation", "-feature", "-unchecked"
        )
    }
    withType<JavaCompile> {
        options.release.set(17)
    }
    test {
        useJUnit()
        workingDir = project.projectDir
    }
}
